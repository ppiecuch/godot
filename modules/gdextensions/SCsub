#!/usr/bin/env python
import os
import version

Import("env")
Import("env_modules")

is_gcc = "gcc" in os.path.basename(env["CC"])
is_clang_or_gcc = (
    ("gcc" in os.path.basename(env["CC"])) or ("clang" in os.path.basename(env["CC"])) or ("osxcross" in env)
)

def is_enabled(m):
    mod = os.path.dirname(m) if os.path.dirname(m) else m
    if "enable_gdextensions_submodules" in env:
        if "none" in env["enable_gdextensions_submodules"]:
            return False
        if not mod in env["enable_gdextensions_submodules"]:
            return False
    env_module.Append(CPPDEFINES=["GDEXT_%s_ENABLED" % mod.upper()])
    print("(GDextensions) Enabling submodule iap")
    return True

env_module = env_modules.Clone()

sources = Glob("*.cpp") + Glob("common/*.cpp")

modules = [
    "core",
    "bulletkit",
    "visual",
    "environment/tree_2d",
    "environment/spherical_waves",
    "environment/water_splash",
    "environment/starfield",
    "environment/spider_anim",
    "fastnoise",
    "smooth",
    "debugdraw",
    "statemachine",
    "benet",
    "behaviornode",
    "settings",
    "sfxr",
    "flashdb",
    "vgamepad",
    "landiscovery",
]

if env["tools"]:
    modules += ["qrcodetexture"]

if not env["disable_3d"]:
    modules += ["environment/vegetation_instance", "ccd/src"]

if os.path.isdir("iap") and is_enabled("iap"):
    env_module.Prepend(CPPPATH=["iap"])
    sources += Glob("iap/*.cpp")
    if env["platform"] == "osx" or env["platform"] == "iphone":
        sources += Glob("iap/mac/*.mm")
        env.Append(LINKFLAGS=["-framework", "StoreKit"])

    elif env["platform"] == "android":
        sources += Glob("iap/android/*.cpp")

ignore = []
if env["platform"] == "3ds" or env["platform"] == "psp":
    ignore += ["visual/heatmap.cpp"]
if env["disable_advanced_gui"]:
    ignore += ["core/input_map_editor.cpp"]
if not env["module_mbedtls_enabled"]:
    ignore += ["core/cripter.cpp"]

for m in modules:
    if is_enabled(m):
        files = Glob("%s/*.cpp" % m)
        sources += [f for f in files if str(f) not in ignore]
        sources += Glob("%s/*.c" % m)

thirdparty_sources = [
    "#thirdparty/misc/cqueue.cpp",
]
if not env["disable_3d"]:
    thirdparty_sources += Glob("#thirdparty/libccd/src/*.c") + Glob("#thirdparty/libccd/src/testsuites/*.c")

env_module.Append(
    CPPPATH=[
        "#thirdparty/enet",
        "#thirdparty/mbedtls/include",
        "#thirdparty/flashdb/inc",
        "#thirdparty/libccd/src",
        "#thirdparty",
        ".",
    ]
)

if env["builtin_freetype"]:
    env_module.Prepend(CPPPATH=["#thirdparty/freetype/include"])
env_module.Append(CPPDEFINES=["GD_CQUEUE", "GD_GODOT_ENET", "GD_NO_UNUSED_FUNCTIONS"])

# Compile as a static library
lib = env_module.Library("gdextensions", thirdparty_sources + sources)
# Add the library as a dependency of the final executable
env.Prepend(LIBS=[lib])
